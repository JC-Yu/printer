## 项目开发日志

### 2023/12/16

调试代码，测试各项功能是否完整正常；移植按键驱动库，添加按键测试功能

### 2023/12/9

跑起FreeRTOS，将前面的驱动程序进行多任务整合

### 2023/12/3

1、缺纸检测模块（查询方式），中断方式没有实现

```c++
#include <Arduino.h>
#include <BLEDevice.h>
#include <BLE2902.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34
#define PAPER_PIN 35
#define VH_EN_PIN 17

#define ADC_FILTERING_COUNT 10

typedef enum
{
  PAPER_STATE_NORMAL = 0,
  PAPER_STATE_LACK,
} PAPER_STATE;

PAPER_STATE paper_state = PAPER_STATE_NORMAL;

void read_paper_state(void)
{
  if(digitalRead(PAPER_PIN) == LOW)
  {
    paper_state = PAPER_STATE_NORMAL;
    Serial.printf("paper normal\n");
  }
  else
  {
    paper_state = PAPER_STATE_LACK;
    Serial.printf("paper lack\n");
  }
}

void setup()
{
  Serial.begin(9600);
  Serial.printf("setup\n");
  // 关闭打印头电源
  pinMode(VH_EN_PIN, OUTPUT);
  digitalWrite(VH_EN_PIN, LOW);
  // 初始化缺纸检测
  pinMode(PAPER_PIN, INPUT);
}


void loop() 
{
  delay(1000);
  read_paper_state();
}
```

2、打印头热敏电阻 温度检测

```c++
#include <Arduino.h>
#include <BLEDevice.h>
#include <BLE2902.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34
#define PAPER_PIN 35
#define VH_EN_PIN 17
#define TEMP_ADC_PIN 36

#define ADC_FILTERING_COUNT 10
#define ADC_RESOLUTION_BIT 12

float em_calculate_temperature(float Rx)
{
  const float B = 3950;
  const float R25 = 30000;
  const float T25 = 25 + 273.15;
  const float offset = 0.5;
  float temp = 1 / (log(Rx/R25)/B + 1/T25) - 273.15 + offset;
  return temp;
}

float get_temperature(void)
{
  const float R1 = 10000;
  float Vi = (float)analogReadMilliVolts(TEMP_ADC_PIN) * (3.3/4095);
  float Rx = (Vi*R1) / (3.3-Vi);
  float temp = em_calculate_temperature(Rx);
  return temp;
}

void setup()
{
  Serial.begin(9600);
  Serial.printf("setup\n");
  // 关闭打印头电源
  pinMode(VH_EN_PIN, OUTPUT);
  digitalWrite(VH_EN_PIN, LOW);

  analogReadResolution(ADC_RESOLUTION_BIT);
}


void loop() 
{
  delay(1000);
  float temperature = get_temperature();
  Serial.printf("temp = %.2f \n", temperature);
}
```

3、打印头步进电机驱动

```c++
#include <Arduino.h>
#include <BLEDevice.h>
#include <BLE2902.h>
#include <Ticker.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34
#define PAPER_PIN 35
#define VH_EN_PIN 17
#define TEMP_ADC_PIN 36
//电机引脚
#define MOTOR_AP_PIN 23
#define MOTOR_AN_PIN 22
#define MOTOR_BP_PIN 21
#define MOTOR_BN_PIN 19

#define ADC_FILTERING_COUNT 10
#define ADC_RESOLUTION_BIT 12
#define MOTOR_WAIT_TIME 2

uint8_t motor_table[8][4] =
{
  {1, 0, 0, 1},
  {0, 0, 0, 1},
  {0, 1, 0, 1},
  {0, 1, 0, 0},
  {0, 1, 1, 0},
  {0, 0, 1, 0},
  {1, 0, 1, 0},
  {1, 0, 0, 0}
};

Ticker motor_timer;
uint8_t motor_pos = 0;

void motor_init(void)
{
  pinMode(MOTOR_AP_PIN, OUTPUT);
  pinMode(MOTOR_AN_PIN, OUTPUT);
  pinMode(MOTOR_BP_PIN, OUTPUT);
  pinMode(MOTOR_BN_PIN, OUTPUT);

  digitalWrite(MOTOR_AP_PIN, 0);
  digitalWrite(MOTOR_AN_PIN, 0);
  digitalWrite(MOTOR_BP_PIN, 0);
  digitalWrite(MOTOR_BN_PIN, 0);
}

void motor_timer_callback(void)
{
  digitalWrite(MOTOR_AP_PIN, motor_table[motor_pos][0]);
  digitalWrite(MOTOR_AN_PIN, motor_table[motor_pos][1]);
  digitalWrite(MOTOR_BP_PIN, motor_table[motor_pos][2]);
  digitalWrite(MOTOR_BN_PIN, motor_table[motor_pos][3]);
  motor_pos++;
  if(motor_pos >= 8)
    motor_pos = 0;
}

void motor_start(void)
{
  if(motor_timer.active() == false)
  {
    motor_timer.attach_ms(MOTOR_WAIT_TIME, motor_timer_callback);
    Serial.printf("motor start now.\n");
  }
}

void motor_stop(void)
{
  digitalWrite(MOTOR_AP_PIN, 0);
  digitalWrite(MOTOR_AN_PIN, 0);
  digitalWrite(MOTOR_BP_PIN, 0);
  digitalWrite(MOTOR_BN_PIN, 0);
  if(motor_timer.active())
  {
    motor_timer.detach();
    Serial.printf("motor stop now.\n");
  }
}


void setup()
{
  Serial.begin(9600);
  Serial.printf("setup\n");
  // 关闭打印头电源
  pinMode(VH_EN_PIN, OUTPUT);
  digitalWrite(VH_EN_PIN, LOW);

  motor_init();
  motor_start();
}


void loop() 
{

}
```

4、打印测试

```c++
#include <Arduino.h>
#include <BLEDevice.h>
#include <BLE2902.h>
#include <SPI.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34
#define PAPER_PIN 35
#define VH_EN_PIN 17
#define TEMP_ADC_PIN 36
// 电机引脚
#define MOTOR_AP_PIN 23
#define MOTOR_AN_PIN 22
#define MOTOR_BP_PIN 21
#define MOTOR_BN_PIN 19

// SPI引脚
#define SPI_SCL_PIN 15
#define SPI_SDA_PIN 13

// 打印头通道
#define STB1_PIN 26  
#define STB2_PIN 27  
#define STB3_PIN 14  
#define STB4_PIN 32  
#define STB5_PIN 33  
#define STB6_PIN 25 

#define ADC_FILTERING_COUNT 10
#define ADC_RESOLUTION_BIT 12
#define MOTOR_WAIT_TIME 2

uint8_t motor_table[8][4] =
{
  {1, 0, 0, 1},
  {0, 0, 0, 1},
  {0, 1, 0, 1},
  {0, 1, 0, 0},
  {0, 1, 1, 0},
  {0, 0, 1, 0},
  {1, 0, 1, 0},
  {1, 0, 0, 0}
};

uint8_t motor_pos = 0;

void motor_init(void)
{
  pinMode(MOTOR_AP_PIN, OUTPUT);
  pinMode(MOTOR_AN_PIN, OUTPUT);
  pinMode(MOTOR_BP_PIN, OUTPUT);
  pinMode(MOTOR_BN_PIN, OUTPUT);

  digitalWrite(MOTOR_AP_PIN, 0);
  digitalWrite(MOTOR_AN_PIN, 0);
  digitalWrite(MOTOR_BP_PIN, 0);
  digitalWrite(MOTOR_BN_PIN, 0);
}

void motor_stop(void)
{
  digitalWrite(MOTOR_AP_PIN, 0);
  digitalWrite(MOTOR_AN_PIN, 0);
  digitalWrite(MOTOR_BP_PIN, 0);
  digitalWrite(MOTOR_BN_PIN, 0);
}

void motor_run_step(uint32_t steps)
{
  while(steps)
  {
    digitalWrite(MOTOR_AP_PIN, motor_table[motor_pos][0]);
    digitalWrite(MOTOR_AN_PIN, motor_table[motor_pos][1]);
    digitalWrite(MOTOR_BP_PIN, motor_table[motor_pos][2]);
    digitalWrite(MOTOR_BN_PIN, motor_table[motor_pos][3]);
    delay(MOTOR_WAIT_TIME);
    motor_pos++;
    if(motor_pos >= 8)
      motor_pos = 0;
    steps--;
  }
}

#define SPI_CLK 1000000

// SPI
SPIClass hspi = SPIClass(HSPI);
SPISettings printer_spi_settings = SPISettings(SPI_CLK, SPI_MSBFIRST, SPI_MODE0);

void spi_init(void)
{
  hspi.begin(SPI_SCL_PIN, 16, SPI_SDA_PIN, -1);
  hspi.setFrequency(2000000);
}

void spi_send_buffer(uint8_t* buffer, uint32_t size)
{
  hspi.beginTransaction(printer_spi_settings);
  hspi.transfer(buffer, size);
  hspi.endTransaction();
}

// 打印模块

#define LAT_PIN 12
#define PRINTER_ONELINE_BYTE 48
#define LAT_TIME 1
#define PRINT_HEAT_TIME 2500
#define PRINT_COOL_TIME 200

void set_stb_idle(void)
{
  digitalWrite(STB1_PIN, LOW);
  digitalWrite(STB2_PIN, LOW);
  digitalWrite(STB3_PIN, LOW);
  digitalWrite(STB4_PIN, LOW);
  digitalWrite(STB5_PIN, LOW);
  digitalWrite(STB6_PIN, LOW);
}

void printer_init(void)
{
  motor_init();
  // 引脚初始化
  pinMode(VH_EN_PIN, OUTPUT);
  pinMode(LAT_PIN, OUTPUT);
  pinMode(SPI_SCL_PIN, OUTPUT);
  pinMode(SPI_SDA_PIN, OUTPUT);
  pinMode(STB1_PIN, OUTPUT);
  pinMode(STB2_PIN, OUTPUT);
  pinMode(STB3_PIN, OUTPUT);
  pinMode(STB4_PIN, OUTPUT);
  pinMode(STB5_PIN, OUTPUT);
  pinMode(STB6_PIN, OUTPUT);
  // 关闭打印头加热通道
  set_stb_idle();
  // 关闭打印头电源
  digitalWrite(VH_EN_PIN, LOW);
  // 初始化spi
  spi_init();
}

void send_oneline_data(uint8_t* data)
{
  spi_send_buffer(data, PRINTER_ONELINE_BYTE);
  digitalWrite(LAT_PIN, LOW);
  delayMicroseconds(LAT_TIME);
  digitalWrite(LAT_PIN, HIGH);
}

void prepare_for_printing(void)
{
  set_stb_idle();
  digitalWrite(LAT_PIN, HIGH);
  digitalWrite(VH_EN_PIN, HIGH);  // 开启打印头电源
}

void printing_stop(void)
{
  digitalWrite(VH_EN_PIN, LOW);   // 关闭打印头电源
  set_stb_idle();
  digitalWrite(LAT_PIN, HIGH);
}

void stb_run(uint8_t stb_num)
{
  switch(stb_num)
  {
    case 0:
      digitalWrite(STB1_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB1_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
    case 1:
      digitalWrite(STB2_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB2_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
    case 2:
      digitalWrite(STB3_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB3_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
    case 3:
      digitalWrite(STB4_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB4_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
    case 4:
      digitalWrite(STB5_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB5_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
    case 5:
      digitalWrite(STB6_PIN, HIGH);
      delayMicroseconds(PRINT_HEAT_TIME);
      digitalWrite(STB6_PIN, LOW);
      delayMicroseconds(PRINT_COOL_TIME);
      break;
  }
}

bool aux_printing_by_single_stb(bool need_stop, uint8_t stb_num)
{
  if(need_stop)
  {
    motor_stop();
    printing_stop();
    return true;
  }
  motor_run_step(1);
  stb_run(stb_num);
  motor_run_step(3);
  return false;
}

void printing_by_single_stb(uint8_t stb_num, uint8_t* buffer, uint32_t length)
{
  uint32_t offset = 0;
  uint8_t* data_ptr = buffer;
  bool need_stop = false;
  prepare_for_printing();
  while(1)
  {
    if(offset < length)
    {
      send_oneline_data(data_ptr);
      offset += 48;
      data_ptr += 48;
    }
    else
      need_stop = true;
    if(aux_printing_by_single_stb(need_stop, stb_num) == true)
      break;
  }
  motor_run_step(40);
  motor_stop();
}

static void setDebugData(uint8_t *print_data)
{
    for (uint32_t index = 0; index < 48 * 5; ++index)
    {
        //0X55 = 0101 0101 0为白，1为黑
        print_data[index] = 0x55;
    }
}

void testSTB()
{
    //每行48byte 1byte=8bit 384bit
    //48*5=5行
    uint8_t print_data[48*5];
    uint32_t print_len;
    Serial.println("start printing test...");
    print_len = 48*5;
    //设置打印的数据内容
    setDebugData(print_data);
    //通道0打印5行
    printing_by_single_stb(0, print_data, print_len);
    setDebugData(print_data);
    //通道1打印5行
    printing_by_single_stb(1, print_data, print_len);
    setDebugData(print_data);
    printing_by_single_stb(2, print_data, print_len);
    setDebugData(print_data);
    printing_by_single_stb(3, print_data, print_len);
    setDebugData(print_data);
    printing_by_single_stb(4, print_data, print_len);
    setDebugData(print_data);
    printing_by_single_stb(5, print_data, print_len);
    Serial.println("test complete.");
}

void setup()
{
  Serial.begin(9600);
  Serial.printf("setup\n");
  printer_init();
}


void loop() 
{
  delay(5000);
  testSTB();
}
```



### 2023/11/29

---

1、编写蓝牙测试程序，实现手机端通过蓝牙与ESP32通讯

```c++
#include <Arduino.h>
#include <BLEDevice.h>
#include <BLE2902.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34

#define ADC_FILTERING_COUNT 10

// Server -> Service -> Charateristic -> Property
#define DEVICE_NAME "mini-printer"
#define SERVICE_UUID "4fafc201-1fb5-459e-8fcc-c5c9c331914b"
#define CHARACTERISTIC_UUID "beb5483e-36e1-4688-b7f5-ea07361b26a8"

bool ble_connected = false;
BLECharacteristic* pCharacteristic;   // 特征，用于发布数据

void ble_report(void)
{
  if(ble_connected)
  {
    uint8_t buffer[5];
    buffer[0] = 1;
    buffer[1] = 2;
    buffer[2] = 3;
    buffer[3] = 4;
    buffer[4] = 5;
    pCharacteristic->setValue(buffer, sizeof(buffer));
    pCharacteristic->notify();
  }
}

class bleServerCallback : public BLEServerCallbacks
{
  void onConnect(BLEServer* pServer)
  {
    ble_connected = true;
    Serial.printf("Device connected\n");
  }

  void onDisconnect(BLEServer* pServer)
  {
    ble_connected = false;
    Serial.printf("Device disconnected\n");
    // 重启广播
    pServer->startAdvertising();
  }
};

class bleCharacteristicCallback : public BLECharacteristicCallbacks
{
  void onRead(BLECharacteristic* pCharacteristic)
  {
    Serial.printf("Client is on read.\n");
  }

  void onWrite(BLECharacteristic* pCharacteristic)
  {
    size_t length = pCharacteristic->getLength();
    uint8_t* data = pCharacteristic->getData();
    Serial.printf("Clent is on write, length = %d \n", length);
  }
};

void ble_init(void)
{
  // 初始化蓝牙设备
  BLEDevice::init(DEVICE_NAME);
  // 创建服务器
  BLEServer* pServer = BLEDevice::createServer();
  // 绑定服务器回调函数(class)
  pServer->setCallbacks(new bleServerCallback());
  // 创建服务
  BLEService* pService = pServer->createService(SERVICE_UUID);
  // 创建特征
  pCharacteristic = pService->createCharacteristic( 
                                  CHARACTERISTIC_UUID, 
                                  BLECharacteristic::PROPERTY_READ
                                | BLECharacteristic::PROPERTY_WRITE
                                | BLECharacteristic::PROPERTY_NOTIFY );
  // 绑定特征回调函数(class)
  pCharacteristic->setCallbacks(new bleCharacteristicCallback());
  // 启动服务
  pService->start();
  // 设备发布数据（广播）
  BLEDevice::startAdvertising();

}

void setup()
{
  Serial.begin(9600);
  ble_init();
  Serial.println("setup");
}

void loop() 
{
  delay(5000);
  ble_report();
}
```

### 2023/11/28

---

1、编写按键驱动程序，实现短按，长按按下，长按释放的判别

```c++
#include <Arduino.h>

#define LED_PIN 18
#define KEY_PIN 5

#define SHORT_PRESS_TIME 1000

typedef enum
{
  SHORT_PRESS = 0,
  LONG_PRESS,
  LONG_PRESS_FREE,
  FREE,
} KEY_STATUS;

bool key_is_press = false;
unsigned long click_time = 0;
bool display_once = false;
KEY_STATUS key_status = FREE;

void key_detect(void)
{
  // 按键没按下时
  if(!key_is_press)
  {
    if(digitalRead(KEY_PIN) == LOW)
    {
      delay(10);
      if(digitalRead(KEY_PIN) == LOW)
      {
        click_time = millis();  // 记录按下时刻
        key_is_press = true;
      }
    }
  }
  // 按键按下了
  if(key_is_press)
  {
    if(digitalRead(KEY_PIN) == HIGH)
    {
      delay(10);
      if(digitalRead(KEY_PIN) == HIGH)
      {
        if(key_status == LONG_PRESS)
        {
          key_status = LONG_PRESS_FREE;
          display_once = false;
          Serial.print("long press free\n");
        }
        else
        {
          key_status = SHORT_PRESS;
          Serial.print("short press free\n");
        }
        key_is_press = false;
        return;
      }
    }
    if(millis() - click_time > SHORT_PRESS_TIME)
    {
      key_status = LONG_PRESS;
      if(!display_once)
      {
        Serial.print("long press hold\n");
        display_once = true;
      }
    }
  }

}

void setup() 
{
  Serial.begin(9600);
  Serial.println("setup");
  pinMode(KEY_PIN, INPUT);
}

void loop() 
{
  key_detect();
}
```

2、电量检测功能：在串口打印电量百分比

```c++
#include <Arduino.h>

#define LED_PIN 18
#define KEY_PIN 5
#define BATTERY_ADC_PIN 34

#define ADC_FILTERING_COUNT 10

uint8_t get_battery_percentage(void)
{
  long sum = 0;
  for(unsigned i=0; i<ADC_FILTERING_COUNT; i++)
    sum += analogReadMilliVolts(BATTERY_ADC_PIN);
  uint32_t voltage = (uint32_t)(sum / ADC_FILTERING_COUNT);
  uint8_t battery = map(voltage*2, 3700, 4200, 0, 100);
  Serial.printf("battery: %d \n", battery);
  return battery;
}


void setup() 
{
  Serial.begin(9600);
  Serial.println("setup");
}

void loop() 
{
  delay(2000);
  get_battery_percentage();
}
```

### 2023/11/27

---

1、完成板卡的上电测试，编写了Hello World程序来验证板卡工作正常

2、编写LED状态指示程序，实现快闪，慢闪，常亮，常灭

```c++
#include <Arduino.h>

#define LED_PIN 18

typedef enum
{
  LED_ON = 0,
  LED_OFF,
  LED_SLOW_FLASH,
  LED_QUICK_FLASH,
} LED_STATUS;

void led_flash(int ms)
{
  digitalWrite(LED_PIN, LOW);
  delay(ms);
  digitalWrite(LED_PIN, HIGH);
  delay(ms);
}

void set_led_status(const LED_STATUS& state)
{
  switch (state)
  {
  case LED_ON:
    digitalWrite(LED_PIN, LOW);
    break;
  case LED_OFF:
    digitalWrite(LED_PIN, HIGH);
    break;
  case LED_SLOW_FLASH:
    led_flash(1000);
    break;
  case LED_QUICK_FLASH:
    led_flash(200);
    break;
  }
}

void setup() 
{
  Serial.begin(9600);
  Serial.println("setup");
  pinMode(LED_PIN, OUTPUT);
}

void loop() 
{
  set_led_status(LED_QUICK_FLASH);
}
```

